#!/usr/bin/env perl
#
# Summary: list all the available perl releases (according to CPAN::Perl::Releases)
#
# Usage: plenv list-perl-releases [-uv]
#
# -n            Suppress the update (or install) of CPAN::Perl::Releases. By
#               default an update/install is attempted every time.
# -v|--verbose  Be very verbose about what is going on. Useful for debugging
#               problems when running this command.
#
# Show every available Perl release. This requires the installation of
# CPAN::Perl::Releases. If that module is not installed the listing will fail.
# If that module is out-of-date the list of Perl releases will be out-of-date.
# Luckily this command will, by default, attempt to update/install that module
# every time it runs. To suppress this functionality pass the -n option.

use strict;
use warnings;

use Getopt::Long;

my $p = 
  Getopt::Long::Parser->new(
    config => [qw(posix_default no_ignore_case pass_through permute bundling)]
  );
$p->getoptions(
  'n'             => \my $noupdate,
  'v|verbose'     => \my $verbose
);

unless( $noupdate ) {
  my $up_cpr = `cpanm CPAN::Perl::Releases`;
  print "$up_cpr\n" if $verbose;
}

eval { require CPAN::Perl::Releases; };
if( $@ ) {
  print "Attempt to load CPAN::Perl::Releases failed:\n$@\n" if $verbose;
  die "For this sub-command to work you must install CPAN::Perl::Releases.\n" .
      "You can do this by running the following command:\n\n" .
      "\$ cpanm CPAN::Perl::Releases\n\n";
}

print "$_\n" foreach CPAN::Perl::Releases::perl_versions();
